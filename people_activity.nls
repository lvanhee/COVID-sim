to execute-activity-effect
  ifelse activity-model = "public&private leisure, rest, age-based work"
  [
    if [gathering-type] of current-activity = "essential-shop"
    [
      let amount-of-rations-to-add amount-of-rations-I-buy-when-going-to-essential-shops
      ask my-home [set available-food-rations available-food-rations + amount-of-rations-to-add ]
    ]
    
    update-resource-based-on-current-activity
    ;might be interesting to hold this variable as an integer, for storing when the next shopping will be needed
    ;additionally, we might want at some point to hold additional variables within homes (i.e. one person getting the food for the whole community)
    
    ;the epistemic infection status depends on whether the person has (had) visible symptoms, not on where it now gathers
    ;though, gathering place might be to be recorded (i.e. a "maybe infected" state if public space has been visited and public space is believed to be contagious)
    ;if [gathering-type] of current-activity = "hospital"
    ;[ set epistemic-infection-status  infection-status ]
    let family-here count ([gatherers] of current-activity) with [member? self [my-relatives] of myself]
    if any? my-relatives and family-here / count my-relatives > 0.5 [ set days-since-seen-relatives 0 ]
    let colleagues-here count ([gatherers] of current-activity) with [member? self [my-colleagues] of myself]
    
    let mc my-colleagues
    
    if any? mc
    [
      if (colleagues-here / count mc > 0.5) [ set days-since-seen-colleagues 0 ]]
    let friends-here count ([gatherers] of current-activity) with [member? self [my-friends] of myself]
    if any? my-friends and friends-here / count my-friends > 0.5 [ set days-since-seen-friends 0 ]
  ]
  [error "not implemented"]
end

to update-resource-based-on-current-activity
  ;if is-at-home? [stop]
  
  if is-working?
  [
    let amount-earned 0
    
    set my-amount-of-resources my-amount-of-resources + amount-earned
    
    ifelse [gathering-type] of current-activity = "essential-shop"
    [
      set amount-earned 10
      ask my-essential-shop [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
    ]
    [
      ifelse [gathering-type] of current-activity = "non-essential-shop"
      [
        set amount-earned 10
        ask my-non-essential-shop [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
      ]
      [
        ifelse [gathering-type] of current-activity = "home"
        [
          set amount-earned 10
          ask my-workplace [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
        ]
        [
          ifelse [gathering-type] of current-activity = "university"
          [
            set amount-earned 10
            ask my-university [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
          ]
          [
            ifelse [gathering-type] of current-activity = "hospital"
            [
              set amount-earned 10
              ask my-hospital [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
            ]
            [
              ifelse [gathering-type] of current-activity = "workplace"
              [
                set amount-earned 10
                ask my-workplace [set amount-of-resources amount-of-resources - amount-earned set current-profit current-profit - amount-earned]
              ]
              [
                error (sentence "not implemented" [gathering-type] of current-activity)
              ]
            ]
          ]
        ]
      ]
    ]
    
    set my-amount-of-resources my-amount-of-resources + amount-earned
    set my-current-income my-current-income + amount-earned
    
    stop
  ]
  
  if is-learning? [stop]
  
  if is-getting-tested? [stop]
  
  if is-resting? [stop]
  
  if is-relaxing? [stop]
  
  if is-performing-essential-shopping?
  [
    let amount-spent amount-of-rations-I-buy-when-going-to-essential-shops
    set my-amount-of-resources my-amount-of-resources - amount-spent
    set my-current-income my-current-income - amount-spent
    ask my-essential-shop [set amount-of-resources amount-of-resources + amount-spent set current-profit current-profit + amount-spent]
    stop
  ]
  
  if is-performing-shopping?
  [
    let amount-spent amount-of-rations-I-buy-when-going-to-shops
    set my-amount-of-resources my-amount-of-resources - amount-spent
    set my-current-income my-current-income - amount-spent
    ask my-non-essential-shop [set amount-of-resources amount-of-resources + amount-spent set current-profit current-profit + amount-spent]
    stop
  ]
  
  error (sentence "not implmplemented" [gathering-type] of current-activity  current-motivation)
end

to-report is-working-at-work?
  report is-working? and is-at-work?
end

to-report is-working?
  report current-motivation = "contractual obligation"
end

;with the people decision update, this function should now only be about execution, not so much about decision [e.g. implement here the effect of getting tested at the hospital]
to perform-activity
  
  ifelse activity-model = "public&private leisure, rest, age-based work" and age-model = "young,student,worker,retired"
  [
    select-activity
    
    ask children with [current-person-I-depend-on != nobody][set current-activity [current-activity] of current-person-I-depend-on]
    
    if not animate? [
      move-to one-of [patches in-radius 3  with [pxcor > [pxcor] of myself]] of current-activity
      set heading towards current-activity
    ]
    
  ]
  [error "not implemented"]
end

to-report is-I-have-contractual-obligations?
  report age = "worker" and (slice-of-the-day = "morning" or slice-of-the-day = "afternoon")
  and not (day-of-the-week = "saturday" or day-of-the-week = "sunday")
end

to-report is-rest-at-home-descriptor? [ad]
  report first ad = my-home and item 1 ad = "rest"
end

to-report is-resting-at-home?
  report is-resting?
  and is-at-home?
end

to-report is-resting? 
  report current-motivation = "rest"
end

to-report is-relaxing? 
  report current-motivation = "relaxing"
end

to-report is-at-home?
  report  current-activity = my-home
end  

to-report is-learning?
  report current-motivation = "learning"
end

to-report is-getting-tested?
  report current-motivation = "get-tested"
end

to-report is-performing-essential-shopping?
  report current-motivation = "essential shopping"
end

to-report is-performing-shopping?
  report current-motivation = "shopping"
end

to-report amount-of-rations-I-buy-when-going-to-essential-shops
  report 7 * count [gatherers] of my-home
end

to-report amount-of-rations-I-buy-when-going-to-shops
  report 7
end

to-report can-be-done-with-a-dependable-person? [ad]
  if [is-home?] of first ad
  [report true]
  if [is-university?] of first ad
  [report false]
  if [is-hospital?] of first ad and last ad = "get-tested"
  [report true]
  if [is-hospital? or is-workplace? or is-shop? or is-school?] of first ad and last ad = "contractual obligation"
  [report false]
  error (sentence "unimplemented" [gathering-type] of first ad last ad)
end