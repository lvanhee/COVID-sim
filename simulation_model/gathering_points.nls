__includes ["hospitals.nls"]
globals
[
  hospitals
  workplaces
  schools
  homes
  private-leisure-points
  public-leisure-points
  universities
  essential-shops
  non-essential-shops
]
breed [gathering-points gathering-point]
gathering-points-own [gathering-type
  available-food-rations ; rations of food that are available, this will be most useful for homes
  
  amount-of-capital
  current-profit
  max-amount-of-capital-to-retain
  
  hired-workers
  stock-of-goods
]


;the head of the link are people and the tail are gathering points
directed-link-breed [gathering-links gathering-link]
gathering-links-own [
  features ;stores whatever is useful for decision-making, useful for including factors appealing to e.g. needs and values
]

to-report ratio-personnel-here
  report count hired-workers with [current-activity = myself and current-motivation = work-motive] / count hired-workers
end

to-report proximity-factor
  if gathering-type = "hospital" [report density-factor-hospital]
  if gathering-type = "school"[report density-factor-schools]
  if gathering-type = "university"[report density-factor-universities]
  if gathering-type = "workplace"[report density-factor-workplaces]
  if gathering-type = "public-leisure"[report density-factor-public-leisure]
  if gathering-type = "private-leisure"[report density-factor-private-leisure]
  if gathering-type = "home"[report density-factor-homes]
  if gathering-type = "essential-shop" [report density-factor-essential-shops]
  if gathering-type = "non-essential-shop" [report density-factor-non-essential-shops]
  error (word gathering-type " not defined")
end

; ToDo: Update this!
to-report is-practicable-at-home?
  if gathering-type = "school" or gathering-type = "university" or gathering-type = "workplace" or gathering-type = "home"
  [report true]
  if gathering-type = "public-leisure" or gathering-type = "private-leisure" or gathering-type = "essential-shop" or gathering-type = "non-essential-shop" or gathering-type = "hospital" 
  [report false]
end


;might be to be revised later as "to-be-expected-to-be-alone-at" based on whether people show(ed) up at the gathering point.
to-report is-alone-at? [gp]
  report any? other [in-gathering-link-neighbors] of gp
end 

to-report is-working-at-home?
  report is-working? and is-at-home?
end

to-report ratio-sick-personnel
  report count hired-workers with [has-internally-visible-symptoms?] / count hired-workers
end

to-report is-at-work?
  if not is-worker? [report false]
  report current-activity = my-work
end

to-report is-home?
  report gathering-type = "home"
end

to-report is-hospital?
  report gathering-type = "hospital"
end

to-report is-workplace?
  report gathering-type = "workplace"
end

to-report is-school?
  report gathering-type = "school"
end

to-report is-essential-shop?
  report gathering-type = "essential-shop"
end

to-report is-non-essential-shop?
  report gathering-type = "non-essential-shop"
end

to-report is-private-leisure?
  report gathering-type = "private-leisure"
end

to-report is-at-private-leisure-place?
  report [is-private-leisure?] of current-activity
end

to-report is-at-public-leisure-place?
  report [is-public-leisure?] of current-activity
end

to-report is-at-university?
  report [is-university?] of current-activity
end

to-report is-public-leisure?
  report gathering-type = "public-leisure"
end

to-report is-shop?
  report is-essential-shop? or is-non-essential-shop?
end

to-report my-university
  report one-of gathering-link-neighbors with [gathering-type = "university"]
end

to-report my-hospital
  report one-of gathering-link-neighbors with [gathering-type = "hospital"]
end


to-report my-school
  report one-of gathering-link-neighbors with [gathering-type = "school"]
end

to-report my-non-essential-shop
  report one-of gathering-link-neighbors with [is-non-essential-shop?]
end

to-report my-essential-shop
  report one-of gathering-link-neighbors with [is-essential-shop?]
end


to-report my-private-leisure
  report one-of gathering-link-neighbors with [is-private-leisure?]
end

to-report my-public-leisure
  report one-of gathering-link-neighbors with [is-public-leisure?]
end

;might be to be revised later, if the number of people expected to show up at the gathering point lowers
to-report expected-number-of-other-participants
 ; if is-practicable-at-home? and is-lockdown-enforced? [report 0]
  if is-hospital? [report random 5 + 2]
  report count other in-gathering-link-neighbors
end

to-report is-accessible?
  if is-lockdown-enforced? [
    report is-open-during-lockdown?
  ]
  if is-workplace? and closed-workplaces? [report false]
  if is-school? and closed-schools? [report false]  
  if is-university? and closed-universities? [report false]  
  report true
end

to-report is-open-during-lockdown?
  if is-workplace? or is-school? or is-university? or is-non-essential-shop? or is-public-leisure?
  or is-private-leisure? [report false]
  if is-essential-shop? or is-hospital? or is-home? [report true]
  
  report (sentence "unimplemented for:" [gathering-type] of self)
end
to-report is-university?
  report gathering-type = "university"
end

to-report gatherers
  report in-gathering-link-neighbors
end

to-report current-gatherers
  report in-gathering-link-neighbors with [current-activity = myself] 
end

to-report empty-houses
  report homes with [not any? gatherers]
end

to-report houses-hosting-retired
  report homes with [any? gatherers with [age = "retired"]]
end

to-report houses-only-hosting-retired
  report homes with [any? gatherers and not any? gatherers with [not (age = "retired")]]
end

to-report houses-hosting-worker
  report homes with [any? gatherers with [age = "worker"]]
end

to-report houses-hosting-retired-couple
  report homes with [count gatherers with [age = "retired"] = 2 and count gatherers = 2]
end

to-report houses-hosting-adults
  report homes with [any? gatherers with [is-adult?]]
end

;;should be renamed in "houses-hosting-non-retied-adults" for instance
to-report houses-hosting-adults2
  report homes with [count gatherers = count gatherers with [age = "worker"] or count gatherers = count gatherers with [age = "student"]]
end

to-report houses-hosting-family
  report homes with [any? gatherers with [age = "worker"] and any? gatherers with [age = "young" or age = "student"] and not any? gatherers with [age = "retired"]]
end

to-report houses-hosting-multiple-generations
  report homes with [any? gatherers with [age = "worker"] and any? gatherers with [age = "young" or age = "student"] and any? gatherers with [age = "retired"]]
end

;to-report closed-schools?
;  report is-closing-school-when-any-reported-case-measure? and is-someone-officially-sick? 
;end


;;might be to be refined if people have a reporting system, or with hospital care
to-report  is-someone-officially-sick? 
  report any? people with [is-believing-to-be-infected?]   
end


to-report gathering-points-total-amount-of-capital
  report sum [amount-of-capital] of gathering-points
end

to-report essential-shop-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "essential-shop"]
end

to-report non-essential-shop-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "non-essential-shop"]
end

to-report university-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "university"]
end

to-report hospital-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "hospital"]
end

to-report school-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "school"]
end

to-report workplace-amount-of-capital
  report sum [amount-of-capital] of gathering-points with [gathering-type = "workplace"]
end

to-report essential-shop-amount-of-goods-in-stock
  report sum [stock-of-goods] of gathering-points with [gathering-type = "essential-shop"]
end

to-report non-essential-shop-amount-of-goods-in-stock
  report sum [stock-of-goods] of gathering-points with [gathering-type = "non-essential-shop"]
end

to-report workplace-amount-of-goods-in-stock
  report sum [stock-of-goods] of gathering-points with [gathering-type = "workplace"]
end

to-report is-leisure-point?
  report is-public-leisure? or is-private-leisure?
end

to-report shops
  report gathering-points with [is-shop?]
end

to-report #essential-shop-workers
  report count workers with [[gathering-type] of my-work = "essential-shop"]
end

to-report #non-essential-shop-workers
  report count workers with [[gathering-type] of my-work = "non-essential-shop"]
end

to-report #university-workers
  report count workers with [[gathering-type] of my-work = "university"]
end

to-report #hospital-workers
  report count workers with [[gathering-type] of my-work = "hospital"]
end

to-report #workplace-workers
  report count workers with [[gathering-type] of my-work = "workplace"]
end

to-report #school-workers
  report count workers with [[gathering-type] of my-work = "school"]
end

to-report is-family-present-in-current-gathering-point?
  report any? my-relatives with [current-activity = [current-activity] of myself]
end