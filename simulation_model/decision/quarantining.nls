to-report is-officially-asked-to-quarantine?
  report #days-I-should-remain-in-self-quarantining > 0 or is-believing-to-be-infected? or [home-in-isolation?] of my-home
end

to update-quarantining-decisions
  ask people with [#days-I-should-remain-in-self-quarantining > 0] [
    set #days-I-should-remain-in-self-quarantining #days-I-should-remain-in-self-quarantining - 1
  ]
end

to quarantining-reaction-when-being-infected
    if decide-to-quarantine-when-symptomatic-when-asked? 
  [include-time-for-quarantining 7 * #ticks-per-day ]
 end

to include-time-for-quarantining [t]
  set #days-I-should-remain-in-self-quarantining max (list t #days-I-should-remain-in-self-quarantining)
end

to quarantining-reaction-when-someone-from-household-is-infected
  if is-infected-and-their-families-requested-to-stay-at-home? and decide-to-quarantine-when-family-member-is-symptomatic-when-asked?
  [include-time-for-quarantining 14 * #ticks-per-day]
  
end

to-report quarantiners-wannabe ; people asked to quarantine? 
  report people with [ is-officially-asked-to-quarantine? ]
end

to-report is-in-quarantine?
  report is-officially-asked-to-quarantine? and (current-activity = my-home or (current-activity = my-hospital and work-motive != current-motivation) or current-activity = away-gathering-point)
end

to-report is-violating-quarantine?
  report is-officially-asked-to-quarantine? and not is-in-quarantine?
end

to process-quarantine-violation-event
    if is-violating-quarantine?[log-file-print (word "violating_quarantine(" ticks "," day-of-the-week "," who "," age "," [who] of current-activity "," [gathering-type] of current-activity "," current-motivation ")")]
end