__includes ["disease_profiles.nls"]
globals [ #people-saved-by-hospitalization
   #people-dying-due-to-lack-of-hospitalization
  #infected
  infected-people
]

to setup-disease-profile
  
    let p random-float 1

    if (age = "retired" and p > probability-self-recovery-symptoms-old + probability-recorvery-if-treated-old + probability-unavoidable-death-old)
    or ((not (age = "retired")) and p > probability-self-recovery-symptoms + probability-recorvery-if-treated + probability-unavoidable-death)
    [
      set disease-profile "self-recovery with no symptoms"
      stop
    ]
    
    if (age = "retired" and p < probability-self-recovery-symptoms-old)
    or ((not (age = "retired")) and p < probability-self-recovery-symptoms)
    [
      set disease-profile "self-recovery with symptoms"
      stop
    ]
    
    ifelse age = "retired" [set p p - probability-self-recovery-symptoms-old]
    [set p p - probability-self-recovery-symptoms]
    
    if (age = "retired" and p < probability-recorvery-if-treated-old)
    or ((not (age = "retired")) and p < probability-recorvery-if-treated)
    [
      set disease-profile "recovery if treated"
      stop
    ]
    
    ifelse age = "retired" [set p p - probability-recorvery-if-treated]
    [set p p - probability-recorvery-if-treated-old]
    
     set disease-profile "unavoidable death"
end


to-report is-immune?
  report infection-status = "immune"
end

to update-within-disease-status   
  if infection-status = healthy-infection-status or infection-status = "immune" [stop]
  if slice-of-the-day = "morning" [set duration-current-disease-status duration-current-disease-status - 1]
  
  ifelse duration-current-disease-status <= 0 
  [
    if infection-status = "asymptomatic noncontagious" 
    [
      set infection-status "infected-asymptomatic"
      set duration-current-disease-status  asympomatic-contagiousness-to-symptomatic-contagiousness
      stop
    ]
    
    if infection-status = infected-asymptomatic-status
    [
      ifelse disease-profile = "self-recovery with no symptoms" [set-immune]
      [
        set infection-status "infected-symptomatic"
        set duration-current-disease-status symptomatic-to-critical-or-heal
      ]
      
      stop
    ]
    
    if infection-status = "infected-symptomatic"
    [
      if disease-profile = recovery-if-treated-profile or disease-profile = "unavoidable death"
      [
        set infection-status "infected-critical"
        set duration-current-disease-status critical-to-terminal
        stop
      ]
      if disease-profile = "self-recovery with symptoms"[set-immune stop]
      error (sentence "unimplemented: " disease-profile duration-current-disease-status infection-status)
    ]
    
    if infection-status = "infected-critical"
    [
      if is-currently-being-treated-at-hospital? and disease-profile = "recovery if treated" 
      [
        set infection-status immune-infection-status
        set #people-saved-by-hospitalization #people-saved-by-hospitalization + 1
        stop
      ]
      
      set infection-status "infected-terminal"
      set duration-current-disease-status terminal-to-death
      stop
    ]
    
    if infection-status = "infected-terminal"
    [kill-person]
    
    error (sentence "unimplemented: " disease-profile duration-current-disease-status infection-status)
  ]
  [stop]
end

to set-immune
  if is-infected?
  [
    set #infected #infected - 1
  ]
    set infection-status immune-infection-status
end

to-report infected-asymptomatic-status
  report "infected-asymptomatic"
end

to set-infection-status [infection]
  if infection = immune-infection-status
  [set-immune]
  
  if not is-infected? and is-infection-status? infection
  [
    set #infected #infected + 1
  ]
  
  set infection-status infection
end

to-report is-infection-status? [in]
  report in = infected-critical-infection-status
  or
  in = infected-asymptomatic-status
end

to-report healthy-infection-status
  report "healthy"
end
to-report is-observing-critical-symptoms?
  report infection-status = infected-critical-infection-status
end

to-report infected-critical-infection-status
  report "infected-critical"
end

to-report immune-infection-status
  report "immune"
end

to kill-person 
  
  set infection-status "dead" 
  set #dead-people #dead-people + 1
  if disease-profile = recovery-if-treated-profile
  [set #people-dying-due-to-lack-of-hospitalization #people-dying-due-to-lack-of-hospitalization + 1]
  if age = "retired" [ set #dead-retired #dead-retired + 1]
  die
end

to-report is-infected?
  report infection-status = "infected"
  or infection-status = "asymptomatic noncontagious" 
  or infection-status = "infected-asymptomatic"
  or infection-status = "infected-symptomatic"
  or infection-status = "infected-critical"
  or infection-status = "infected-terminal"
end

;simplification, with a more advanced model, these symptoms might be visible after a few days of incubation
;might depend on age (e.g. more visible for the elderly, might be undetected for kids)
to-report has-internally-visible-symptoms?
  report  infection-status = "infected-symptomatic"
  or infection-status = "infected-critical"
  or infection-status = "infected-terminal"
end


to update-within-agent-disease-status
  ask people [update-within-disease-status]
end
